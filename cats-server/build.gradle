buildscript {
	ext {
		springBootVersion = '1.3.5.RELEASE'
	}
	repositories {
		mavenCentral()
		maven {url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("gradle.plugin.com.boxfuse.client:flyway-release:4.0.3")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'
apply plugin: 'org.flywaydb.flyway'


jar {
	baseName = 'cats-server'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-data-mongodb')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.postgresql:postgresql:9.4.1207')
	compile('org.springframework.boot:spring-boot-devtools')
	compile('com.mangofactory:swagger-springmvc:1.0.2')
	compile('com.wordnik:swagger-annotations')
	compile('com.mangofactory:swagger-models')

	//compile('org.flywaydb:flyway-core:4.0.3')

	testCompile('org.springframework.boot:spring-boot-starter-test') 
}


eclipse {
	classpath {
		 containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		 containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
	}
}

/* This task expects a parameter called 'mf' for migration file description
   Usage: gradle mkm -Pmf=AddNewColumnToTable
   Note: Migration name should be in CamelCase
*/
task mkm <<{
	description = 'Creates a new migraiton file for flyway. Usage: gradle mkm -Pmf=AddNewColumnToTable'
	def fileName = mf.replaceAll(/\B[A-Z]/) { '_' + it }.toLowerCase()
	def date = new Date()
	def formattedDate = date.format('yyyyMMddHHmmss')
	def migrationFileName = "V${formattedDate}__${fileName}.sql"
	def file = new File("${projectDir}/src/main/resources/db/migration/${migrationFileName}")
	file.text = "-- Migration file ${migrationFileName} created at --"
	println "Generated new migration file ${migrationFileName}"
}

flyway{
	url = 'jdbc:postgresql://localhost:5432/cats_dev'
	user = 'cats'
	password = 'cats'
}